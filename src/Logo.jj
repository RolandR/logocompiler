// Author: Olivier Biberstein
// Date: 2015-12-09
//
// Parser and translator of a subset of Logo into Java

options {
   LOOKAHEAD = 2;
   FORCE_LA_CHECK = true;
   DEBUG_PARSER = true;
   DEBUG_TOKEN_MANAGER = true;
}

PARSER_BEGIN(Logo)

import java.lang.*;
import java.io.*;
import java.util.*;

public class Logo {

  static private File javaFile, htmlFile;   // output files
  static private PrintWriter pw;            // printwriter used for every output
  static private int numIndent = 0;         // size of indentation
  static private int repcountNum = 0;       // number for multiple REPEATs

  // to pretty-print the translation
  // uses numIndent and pw defined as static variables in LogoParser
  // writes in pw the numIndent times the indent string
  public static void indent() {
    for (int i=0; i<numIndent; i++) { pw.print("   "); }
  }

  // Main method that reads the source file ".logo" and translates it
  // into two files: ".java" ".html"
  public static void main(String args[]) throws ParseException,
                                                TokenMgrError,
                                                IOException
  {
    // reads the source file ".logo" (first argument of command line)
    BufferedReader in = new BufferedReader(new FileReader(args[0]));
    Logo parser = new Logo(in);
    try {
      parser.start();
      System.out.println("DONE");
    }
    catch (ParseException x) { System.out.println("Syntaxtic Error"); throw x; }
    catch (TokenMgrError x)  { System.out.println("Lexical Error"); throw x; }
  }
}
PARSER_END(Logo)

// skip separators
SKIP : { <" " | "\t" | "\n" | "\r"> }
// skip comments of the form "# .... \n"
SKIP : { <"#" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> }

// Constants, pre-defined, operators etc.
TOKEN: { <ADD: "+">   | <SUB: "-">  | <MUL: "*"> | <DIV: "/"> }
TOKEN: { <LPAR: "(">  | <RPAR: ")"> | <#PARSEP: ":"> }
TOKEN: { <LBRA: "[">  | <RBRA: "]"> }
TOKEN: { <EQ: "==">   | <NE: "!=">  }
TOKEN: { <LT: "<">    | <GT: ">">   | <LE: "<="> | <GE: ">="> }
TOKEN: { <AND: "AND"> | <OR: "OR"> }
TOKEN: { <TRUE: "TRUE"> | <FALSE: "FALSE"> | <NOT: "NOT"> }
// REPCOUNT is a pre-defined identifier rather than a keyword
TOKEN: { <REPCOUNT: "REPCOUNT"> }
// Keywords
TOKEN: {
  <CLEARSCREEN: "CS"> |
  <PENDOWN: "PD">     | <PENUP: "PU">      |
  <FORWARD: "FD">     | <BACKWARD: "BK">   |
  <LEFTTURN: "LT">    | <RIGHTTURN: "RT">  |
  <HIDETURTLE: "HT">  | <SHOWTURTLE: "ST"> |
  <WAIT: "WAIT">      | <REPEAT: "REPEAT"> |
  <IF: "IF">          | <IFELSE: "IFELSE"> |
  <SUBROUTINE: "TO">  |
  <LOGO: "LOGO">      | <END: "END">
}
// Numerical values, identifiers, and parameters
TOKEN: { <#DIGIT:  ["0"-"9"]> }
TOKEN: { <#LETTER: ["A"-"Z"]> }
TOKEN: { <NUM: (<DIGIT>)+ | (<DIGIT>)+ "." (<DIGIT>)+ > }
TOKEN: { <IDENTIFIER: <LETTER> ( <LETTER> | <DIGIT> )* > }
TOKEN: { <PARAMETER: <PARSEP> <IDENTIFIER> > // removes parameter separator
          { matchedToken.image=image.substring(1,image.length()); } }

void start() throws IOException:
{
	Token t;
	String s;
}
{
  // logo programs start with LOGO followed by an identifier
  <LOGO>  t = <IDENTIFIER>
  // Creation of the output files
  {
    // Create the HTML file for the applet
    htmlFile = new File(t.image.toLowerCase() + ".html");
    pw = new PrintWriter(new FileOutputStream(htmlFile)); 
    pw.println("<html>");
    pw.println("  <body>");
    pw.println("  <applet code='" + t.image.toLowerCase() +
               ".class'width=600 height=600></applet>");
    pw.println("  </html>");
    pw.println("</body>");
    pw.close();
    //Create the Java file and the class
    javaFile = new File(t.image.toLowerCase() + ".java");
    pw = new PrintWriter(new FileOutputStream(javaFile));
    pw.println("import java.awt.Graphics;\n");
    pw.println("public class " + t.image.toLowerCase() +
               " extends java.applet.Applet {\n" );
    numIndent++;
    indent();
    pw.println("private LogoPrimitives logo;\n");
  }
  
   ( subroutine() )*
   // Open the necessary method "paint" of the applet
   
   {
     indent(); numIndent++;
     pw.println("public void paint(Graphics g) {");
     indent();
     pw.println("logo = new LogoPrimitives(this);");
     pw.println();
   }
   
   (s = statement() {
	   indent();
	   pw.println("logo."+s);
	})*
   
   // close the method "paint"
   {
      numIndent--;
      indent();
      pw.println("}");
   }
   //close the class
	{
	  pw.flush();
	}
  
  <END>                 { numIndent--; indent();
                          pw.println("}");
                          pw.flush(); pw.close(); }
}

void subroutine():
{
	Token i;
}
{
	<SUBROUTINE>
	i = <IDENTIFIER>
	(<PARAMETER>)*
	(statement())*
	<END>
}

String statement():
{
	String s = "";
	String param = "";
}
{
	(
		// Simple Statements
		 <CLEARSCREEN> {s = "cs();";}
		|<PENDOWN> {s = "pd();";}
		|<PENUP> {s = "pu();";}
		|<HIDETURTLE> {s = "et();";}
		|<SHOWTURTLE> {s = "dt();";}
		// Statements with one parameter
		|<FORWARD> param = nExpr() {s = "fd("+param+");";}
		|<BACKWARD> param = nExpr() {s = "bk("+param+");";}
		|<LEFTTURN> param = nExpr() {s = "rt("+param+");";}
		|<RIGHTTURN> param = nExpr() {s = "lt("+param+");";}
		|<WAIT> nExpr()
		// More complex statements
		|<REPEAT> nExpr() <LBRA> (statement())* <RBRA>
		|<IF> bExpr() <LBRA> (statement())* <RBRA>
		|<IFELSE> bExpr() <LBRA> (statement())* <RBRA> <LBRA> (statement())* <RBRA>
		|<IDENTIFIER> (nExpr())*
	)
	{return s;}
}

String nExpr():
{
	String exp;
	String term;
}
{
	exp = nTerm() ((<ADD> {exp += " + ";} | <SUB> {exp += " - ";}) term = nTerm() {exp += term;})*
	
	{return exp + " ";}
}

String nTerm():
{
	String term;
	Token sign;
	String factor;
}
{
	term = nFactor() ((sign = <MUL> | sign = <DIV>) factor = nFactor() {term += sign.image + factor;})*
	{return term;}
}

String nFactor():
{
	String factor = "";
	String e;
	Token t = null;
}
{
	(<SUB> {factor += "-";})? (t = <NUM> | t = <REPCOUNT> | t = <PARAMETER> | <LPAR> e = nExpr() <RPAR> {factor += "("+e+")";})
	{
		if(t != null){
			factor += t.image;
		}
		return factor;
	}
}

String bExpr():
{
	String expr;
	String term;
	Token comp;
}
{
	expr = bTerm() (comp = <OR> term = bTerm() {expr += comp.image + term;})*
	{return expr;}
}

String bTerm():
{
	String term;
	String factor;
	Token comp;
}
{
	term = bFactor() (comp = <AND> factor = bFactor() {term += comp.image + factor;} )*
	{return term;}
}

String bFactor():
{
	String factor = "";
	String expr;
	Token comp;
	Token literal;
}
{
	(
		 literal = <TRUE> {return literal.image;}
		|literal = <FALSE> {return literal.image;}
		|<NOT> <LPAR> expr = bExpr() <RPAR> {factor = "!("+expr+")";}
		|factor = nExpr() (comp = <EQ> | comp = <NE>| comp = <LT> | comp = <GT> | comp = <LE> | comp = <GE>) expr = nExpr() {factor += comp.image + expr;}
	)
	
	{return factor;}
}





















